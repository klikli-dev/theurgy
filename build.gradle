plugins {
    id 'idea'
    id 'maven-publish'
    id 'net.neoforged.gradle.userdev' version "${neogradle_version}"
}

if (System.getenv('MOD_VERSION') != null) {
    mod_version = System.getenv('MOD_VERSION')
}
version = mod_version

base {
    archivesName = "${mod_id}-${minecraft_version}-neoforge"
}

//TODO: re enable source sets once mods are updated
sourceSets {
    main {
        java {
            exclude '**/AlmostUnifiedIntegrationImpl.java'
//            exclude '**/jade/**/*.java'
//            exclude '**/jei/**/*.java'
        }
    }
}

java.toolchain.languageVersion = JavaLanguageVersion.of(21)
java.withSourcesJar()

if (file('src/main/resources/META-INF/accesstransformer.cfg').exists()) {
    minecraft.accessTransformers.file file('src/main/resources/META-INF/accesstransformer.cfg')
}

runs {
    configureEach {
        systemProperty 'forge.logging.markers', 'REGISTRIES'
        systemProperty 'forge.logging.console.level', 'debug'
        jvmArguments.addAll('-XX:+AllowEnhancedClassRedefinition', '-XX:+IgnoreUnrecognizedVMOptions')
        modSource project.sourceSets.main
    }

    client {
        systemProperty 'forge.enabledGameTestNamespaces', project.mod_id
        programArgument "--username=Dev"
    }

    server {
        systemProperty 'forge.enabledGameTestNamespaces', project.mod_id
//        programArgument '--nogui'
    }

    data {
        programArguments.addAll '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
    }
}

// Include resources generated by data generators.
sourceSets.main.resources { srcDir 'src/generated/resources' }

// Sets up a dependency configuration called 'localRuntime'.
// This configuration should be used instead of 'runtimeOnly' to declare
// a dependency that will be present for runtime testing but that is
// "optional", meaning it will not be pulled by dependents of this mod.
configurations {
    runtimeClasspath.extendsFrom localRuntime
}


repositories {
    mavenLocal()
    maven {
        url "https://dl.cloudsmith.io/public/klikli-dev/mods/maven/"
        content {
            includeGroup "com.klikli_dev"
        }
    }
    maven {
        name = "JEI and AlmostUnified"
        url = 'https://maven.blamejared.com/'
        content {
            includeGroup "mezz.jei"
            includeGroup "com.almostreliable.mods"
        }
    }
    maven {
        name = 'GeckoLib'
        url = 'https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/'
        content {
            includeGroup "software.bernie.geckolib"
        }
    }
    maven {
        name = "OctoStudios (Curios Api Contiunation)"
        url = uri("https://maven.octo-studios.com/releases")
        content {
            includeGroup "top.theillusivec4.curios"
        }
    }
    maven {
        name = "SBL Maven"
        url "https://dl.cloudsmith.io/public/tslat/sbl/maven/"
        content {
            includeGroup "net.tslat.smartbrainlib"
        }
    }
    maven {
        // Shedaniel's maven (Architectury API)
        url = "https://maven.architectury.dev"
        content {
            includeGroup "dev.architectury"
        }
    }
    maven {
        // saps.dev Maven (KubeJS and Rhino)
        url = "https://maven.saps.dev/releases"
        content {
            includeGroup "dev.latvian.mods"
        }
    }
    maven {
        url "https://cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
    maven {
        name = "Modrinth"
        url = "https://api.modrinth.com/maven"
        content {
            includeGroup "maven.modrinth"
        }
    }
    maven {
        name = "TerraformersMC (emi)"
        url = "https://maven.terraformersmc.com/"
        content {
            includeGroup "dev.emi"
        }
    }
}

dependencies {
    implementation "net.neoforged:neoforge:${neo_version}"

    //TODO: enable once available
    //Jei
    compileOnly "mezz.jei:jei-${minecraft_version_major}-common-api:${jei_version}"
    compileOnly "mezz.jei:jei-${minecraft_version_major}-common:${jei_version}"
//    //we're using the vanilla recipe category rendering constants
    compileOnly "mezz.jei:jei-${minecraft_version_major}-neoforge-api:${jei_version}"
//    runtimeOnly "mezz.jei:jei-${minecraft_version}-neoforge:${jei_version}"

    //TODO: enable once available
    //almostunified
//    implementation "com.almostreliable.mods:almostunified-forge:${minecraft_version}-${almost_unified_version}"

    //Geckolib
    implementation ("software.bernie.geckolib:geckolib-neoforge-${minecraft_version_major}:${geckolib_version}") {transitive=false}

    //modonomicon
    implementation ("com.klikli_dev:modonomicon-${minecraft_version}-neoforge:${modonomicon_version}") {transitive=false}

    //occultism
    compileOnly ("com.klikli_dev:occultism-${minecraft_version}-neoforge:${occultism_version}") {transitive=false}
//    runtimeOnly ("com.klikli_dev:occultism-${minecraft_version}-neoforge:${occultism_version}")

    //Jade (Hwyla/Waila)
    implementation ("maven.modrinth:jade:${jade_version}+neoforge") {transitive=false}

    //EMI
    compileOnly ("dev.emi:emi-neoforge:${emi_version}+${minecraft_version_major}:api") {transitive=false}
    runtimeOnly ("dev.emi:emi-neoforge:${emi_version}+${minecraft_version_major}") {transitive=false}
}

processResources {
    filesMatching(['META-INF/neoforge.mods.toml']) {
        expand project.properties
    }
}

// Example for how to get properties into the manifest for reading at runtime.
jar {
    manifest {
        attributes([
                "Specification-Title"     : mod_id,
                "Specification-Vendor"    : mod_authors,
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : mod_version,
                "Implementation-Vendor"   : mod_authors,
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

jar { exclude ".cache" }

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId base.archivesName.get()

            from components.java

            pom {
                name = mod_name
                description = 'An open-source magic mod built around classical alchemy to replicate and transmute matter, and create magical artifacts.'
                url = 'https://github.com/klikli-dev/theurgy'
                licenses {
                    license {
                        name = 'MIT AND CC-BY-4.0'
                        url = 'https://github.com/klikli-dev/theurgy#licensing'
                    }
                }
                scm {
                    connection = 'scm:git:ssh:git@github.com:klikli-dev/theurgy.git'
                    url = 'https://github.com/klikli-dev/theurgy'
                }
            }
        }
    }
    repositories {
        maven {
            name = "cloudsmith"
            url = "https://maven.cloudsmith.io/klikli-dev/mods/"
            def releasesRepoUrl = "https://maven.cloudsmith.io/klikli-dev/mods/"
            def snapshotsRepoUrl = "https://maven.cloudsmith.io/klikli-dev/mods/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = System.getenv('MAVEN_USER')
                password = System.getenv('MAVEN_PASS')
            }
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}

//increase max errors from 100 to 2000
afterEvaluate {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xmaxerrs" << "2000"
    }
}

// IDEA no longer automatically downloads sources/javadoc jars for dependencies, so we need to explicitly enable the behavior.
idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}